spec is "1.0"

identity "User":
  field "email" is text must be present
  field "role" is text must be present

record "ParallelResult":
  field "run_id" is text must be present
  field "agent" is text must be present
  field "output" is text must be present

ai "parallel_ai":
  provider is "mock"
  model is "mock-model"
  system_prompt is "Return brief, deterministic replies."

agent "alpha":
  ai is "parallel_ai"
  system_prompt is "Handle part A."

agent "beta":
  ai is "parallel_ai"
  system_prompt is "Handle part B."

agent "gamma":
  ai is "parallel_ai"
  system_prompt is "Handle part C."

flow "run_parallel_demo": requires identity.role is "operator"
  let task is "Summarize pipeline status."
  let run_id is "run-001"
  run agents in parallel:
    agent "alpha" with input: task
    agent "beta" with input: task
    agent "gamma" with input: task
  as feedback
  let first is list get feedback at 0
  let second is list get feedback at 1
  let third is list get feedback at 2
  let merged is list:
    map:
      "agent" is "alpha"
      "output" is first
    map:
      "agent" is "beta"
      "output" is second
    map:
      "agent" is "gamma"
      "output" is third
  for each item in merged:
    set state.result with:
      run_id is run_id
      agent is item.agent
      output is item.output
    create "ParallelResult" with state.result as result
  return list length of merged

page "home": requires identity.role is "operator"
  title is "Parallel Coordinator"
  text is "Run agents in parallel and merge deterministically."
  button "Run parallel":
    calls flow "run_parallel_demo"
  table is "ParallelResult"
